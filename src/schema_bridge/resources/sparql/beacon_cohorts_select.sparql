PREFIX field: <https://catalogue.org/field/>
PREFIX entity: <https://catalogue.org/entity/>

SELECT
  ?id
  ?name
  ?cohortType
  ?cohortDesign
  ?cohortSize
  ?ageStart
  ?ageEnd
  (GROUP_CONCAT(?location;separator="|") AS ?locations)
  (GROUP_CONCAT(?gender;separator="|") AS ?genders)
  (GROUP_CONCAT(?dataType;separator="|") AS ?cohortDataTypes)
WHERE {
  ?res a entity:Cohort ;
       field:cohortId ?id ;
       field:cohortName ?name .
  OPTIONAL { ?res field:cohortType ?cohortType . }
  OPTIONAL { ?res field:cohortDesign ?cohortDesign . }
  OPTIONAL { ?res field:cohortSize ?cohortSize . }
  OPTIONAL { ?res field:inclusionCriteria_ageRange_start_iso8601duration ?ageStart . }
  OPTIONAL { ?res field:inclusionCriteria_ageRange_end_iso8601duration ?ageEnd . }
  OPTIONAL { ?res field:locations ?location . }
  OPTIONAL { ?res field:genders ?gender . }
  OPTIONAL { ?res field:cohortDataTypes ?dataType . }
}
GROUP BY ?id ?name ?cohortType ?cohortDesign ?cohortSize ?ageStart ?ageEnd
