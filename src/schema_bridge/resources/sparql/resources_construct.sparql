PREFIX field: <https://catalogue.org/field/>
PREFIX entity: <https://catalogue.org/entity/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>

CONSTRUCT {
  ?res a dcat:Dataset ;
       dct:identifier ?id ;
       dct:title ?name ;
       dct:description ?desc ;
       dcat:landingPage ?websiteIri ;
       dcat:contactPoint ?contact .
  ?contact a vcard:Individual ;
           vcard:hasEmail ?emailIri .
}
WHERE {
  ?res a entity:Resource ;
       field:id ?id ;
       field:name ?name ;
       field:description ?desc .
  OPTIONAL {
    ?res field:website ?website .
    BIND(IRI(STR(?website)) AS ?websiteIri)
  }
  OPTIONAL {
    ?res field:contactEmail ?email .
    BIND(STR(?email) AS ?emailRaw)
    BIND(IF(CONTAINS(?emailRaw, ","), STRBEFORE(?emailRaw, ","), ?emailRaw) AS ?emailTmp)
    BIND(IF(CONTAINS(?emailTmp, ";"), STRBEFORE(?emailTmp, ";"), ?emailTmp) AS ?emailTmp2)
    BIND(REPLACE(?emailTmp2, "^mailto:", "") AS ?emailClean)
    BIND(IRI(CONCAT("mailto:", ?emailClean)) AS ?emailIri)
    BIND(IRI(CONCAT("https://catalogue.org/contact/", ENCODE_FOR_URI(?emailClean))) AS ?contact)
  }
}
