PREFIX ex: <https://catalogue.org/>
PREFIX field: <https://catalogue.org/field/>
PREFIX entity: <https://catalogue.org/entity/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dct: <http://purl.org/dc/terms/>

CONSTRUCT {
  ?res a dcat:Dataset ;
       dct:title ?title ;
       dct:identifier ?identifier ;
       dct:description ?desc ;
       dcat:landingPage ?landingPageIri ;
       dcat:keyword ?keyword ;
       dcat:theme ?themeIri .
}
WHERE {
  ?res a ?entity .
  VALUES ?entity { entity:Resource entity:Dataset }
  OPTIONAL { ?res field:title ?titleValue . }
  OPTIONAL { ?res field:name ?nameValue . }
  BIND(COALESCE(?titleValue, ?nameValue) AS ?title)
  FILTER(BOUND(?title))
  OPTIONAL { ?res field:description ?desc . }
  OPTIONAL { ?res field:id ?identifier . }
  OPTIONAL {
    ?res (field:landingPage|field:website) ?landingPage .
    BIND(IRI(STR(?landingPage)) AS ?landingPageIri)
  }
  OPTIONAL { ?res field:keyword ?keyword . }
  OPTIONAL {
    ?res field:theme ?theme .
    BIND(IRI(STR(?theme)) AS ?themeIri)
  }
}
